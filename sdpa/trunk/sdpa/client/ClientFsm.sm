%{
//
// Finite state machine for the SDPA client
//

#if __GNUC__ >4 || ( __GNUC__==4 && __GNUC_MINOR__ > 1)
#  pragma GCC diagnostic ignored "-Wall"
#  pragma GCC diagnostic ignored "-Wunused"
#  pragma GCC diagnostic ignored "-Weffc++"
#  pragma GCC diagnostic ignored "-Wunused-parameter"
#endif

%}

%package sdpa::client
%class Client
%header Client.hpp


%include <sdpa/types.hpp>
%include <sdpa/client/types.hpp>
%include <seda/IEvent.hpp>

%start ClientFsm::Unconfigured

%map ClientFsm
%%
Unconfigured
{
  Start(cfg: const sdpa::client::config_t &)
    Configuring
    {
      action_configure(cfg);
    }

  Shutdown
    nil
    {
      action_shutdown();
    }

  Default
    nil
    {
    }
}

Configuring
{
  ConfigOk(evt: const seda::IEvent::Ptr &)
    Configured
    {
      action_config_ok();
      action_store_reply(evt);
    }

  ConfigNok(evt: const seda::IEvent::Ptr &)
    Unconfigured
    {
      action_config_nok(); // exit
      action_store_reply(evt);
    }

  Default
    nil
    {
    }
}

Configured
{
  // client side api
  Submit(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_submit(evt);
    }
    
   Subscribe(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_subscribe(evt);
    }

  Cancel(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_cancel(evt);
    }

  Query(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_query(evt);
    }

  Retrieve(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_retrieve(evt);
    }

  Delete(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_delete(evt);
    }

  Shutdown
    nil
    {
      action_shutdown();
    }

  // replies (i.e. pseudo callbacks) from daemon
  SubmitAck(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_store_reply(evt);
    }
    
	SubscribeAck(evt: const seda::IEvent::Ptr &)
	nil
    {
      action_store_reply(evt);
    }
   
  CancelAck(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_store_reply(evt);
    }
  DeleteAck(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_store_reply(evt);
    }
  Results(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_store_reply(evt);
    }

  StatusReply(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_store_reply(evt);
    }
  Error(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_store_reply(evt);
    }

  Unknown(evt: const seda::IEvent::Ptr &)
    nil
    {
      action_store_reply(evt);
    }
}

%%
