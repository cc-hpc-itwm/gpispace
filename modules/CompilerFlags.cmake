# definitions
include (CheckCXXCompilerFlag)

if (${CMAKE_BUILD_TYPE} MATCHES "Release")
  add_definitions ("-DNDEBUG")
endif()

set (CMAKE_CXX_STANDARD 11)
set (CMAKE_CXX_STANDARD_REQUIRED on)

macro (CHECK_AND_ADD_COMPILER_FLAG _VAR _FLAG)
  STRING(REGEX REPLACE "-" _ __flag_literal ${_FLAG})
  set(__flag_literal "FLAG${__flag_literal}")
  CHECK_CXX_COMPILER_FLAG (${_FLAG} ${__flag_literal})
  if (${__flag_literal})
     set (${_VAR} "${${_VAR}} ${_FLAG}")
  endif ()
endmacro ()

# warnings
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -W)
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wall)
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wextra)
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS_CXX -Wnon-virtual-dtor)
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wno-attributes)
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wno-system-headers)
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wno-unknown-pragmas)
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Werror=extra-semi)

if (${CMAKE_CXX_COMPILER_ID} MATCHES "GNU")
  CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wno-format)
  CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wno-ignored-qualifiers)
  CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -Wno-write-strings)
endif()

if (${CMAKE_CXX_COMPILER_ID} MATCHES "Intel")
  set (FLAGS_WARNINGS "${FLAGS_WARNINGS} -diag-disable 191")
  set (FLAGS_WARNINGS "${FLAGS_WARNINGS} -diag-disable 1170")
  set (FLAGS_WARNINGS "${FLAGS_WARNINGS} -diag-disable 1292")
  set (FLAGS_WARNINGS "${FLAGS_WARNINGS} -diag-disable 2196")
endif()

# to avoid warnings when using gcc 4.5
CHECK_AND_ADD_COMPILER_FLAG (FLAGS_WARNINGS -fno-strict-aliasing)

# other flags

CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS -fpic)
CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS -fPIC)

CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_RELEASE -O3)

if (${CMAKE_CXX_COMPILER_ID} MATCHES "GNU")
  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_RELEASE -fstack-protector-all)

  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_DEBUG -O0)
  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_DEBUG -ggdb)
  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_DEBUG -fno-omit-frame-pointer)

  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_PROFILE -O0)
  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_PROFILE -ggdb)
  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_PROFILE -pg)
  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_PROFILE -fprofile-arcs)
  CHECK_AND_ADD_COMPILER_FLAG (C_FLAGS_PROFILE -ftest-coverage)

  # TODO: Are these required?!
  set(CMAKE_SHARED_LINKER_FLAGS_RELWITHDEBINFO
    "${CMAKE_SHARED_LINKER_FLAGS_RELWITHDEBINFO} -rdynamic")
  set(CMAKE_SHARED_LINKER_FLAGS_DEBUG
    "${CMAKE_SHARED_LINKER_FLAGS_DEBUG} -rdynamic")
endif ()

if (${CMAKE_CXX_COMPILER_ID} MATCHES "Clang")
  set (C_FLAGS "${C_FLAGS} -ftemplate-depth=1024")

  if ("${CMAKE_GENERATOR}" STREQUAL "Ninja")
    set (C_FLAGS "${C_FLAGS} -fcolor-diagnostics")
  endif()
endif()


# assemble

set (C_FLAGS "${C_FLAGS} ${FLAGS_WARNINGS}")

set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${C_FLAGS} ${CXX_FLAGS} ${FLAGS_WARNINGS_CXX}")
set (CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} ${C_FLAGS_DEBUG}")
set (CMAKE_CXX_FLAGS_PROFILE "${CMAKE_CXX_FLAGS_PROFILE} ${C_FLAGS_PROFILE}")
set (CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} ${C_FLAGS_RELEASE}")

set (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${C_FLAGS}")
set (CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} ${C_FLAGS_DEBUG}")
set (CMAKE_C_FLAGS_PROFILE "${CMAKE_C_FLAGS_PROFILE} ${C_FLAGS_PROFILE}")
set (CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} ${C_FLAGS_RELEASE}")
