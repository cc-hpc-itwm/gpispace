###############################################################################
# CMake minimum required version verification
###############################################################################

if (CMAKE_MINIMUM_REQUIRED_VERSION VERSION_LESS @CMAKE_MINIMUM_REQUIRED_VERSION@)
  set (${CMAKE_FIND_PACKAGE_NAME}_FOUND false)
  set (${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
    "The minimum required CMake version of the including project is too low! \
    Projects using ${CMAKE_FIND_PACKAGE_NAME} are required to set CMake version \
    @CMAKE_MINIMUM_REQUIRED_VERSION@ or above as their minimum required version! \
    A line of code such as \
    \
      cmake_minimum_required(VERSION @CMAKE_MINIMUM_REQUIRED_VERSION@)'\
    \
    is required to be added at the top of the including project's root CMakeLists.txt."
  )
  return ()
endif ()

###############################################################################
# Inclusion guard
###############################################################################

if (${CMAKE_FIND_PACKAGE_NAME}_FOUND)
  return ()
endif ()

@PACKAGE_INIT@

###############################################################################
# Includes
###############################################################################

include (CMakeFindDependencyMacro)
include (GNUInstallDirs)

###############################################################################
# Library paths
###############################################################################

set_and_check (UTIL_GENERIC_HOME "@PACKAGE__origin@")
get_filename_component (UTIL_GENERIC_HOME "${UTIL_GENERIC_HOME}" REALPATH)

###############################################################################
# Dependencies
###############################################################################

find_dependency (Boost @Boost_VERSION@ EXACT REQUIRED
  COMPONENTS
    filesystem
    program_options
    serialization
    system
    thread
    unit_test_framework
)

###############################################################################
# Targets
###############################################################################

add_library (Util::Generic-Headers INTERFACE IMPORTED)
target_include_directories (Util::Generic-Headers
  INTERFACE "${UTIL_GENERIC_HOME}/${CMAKE_INSTALL_INCLUDEDIR}"
)
target_link_libraries (Util::Generic-Headers
  INTERFACE
    Boost::headers
    Boost::serialization
    Boost::filesystem
    Boost::system
)
target_compile_features (Util::Generic-Headers
  INTERFACE "@_project_cxx_std@"
)

add_library (Util::Generic STATIC IMPORTED)
set_target_properties (Util::Generic
  PROPERTIES
    IMPORTED_LOCATION "${UTIL_GENERIC_HOME}/lib/libUtil-Generic.a"
)
target_link_libraries (Util::Generic
  INTERFACE
    Util::Generic-Headers
    Boost::headers
    Boost::serialization
    Boost::filesystem
    Boost::unit_test_framework
    Boost::system
    ${CMAKE_DL_LIBS}
    rt
    Threads::Threads
)
target_compile_features (Util::Generic
  INTERFACE "@_project_cxx_std@"
)
