%{
  // Finite state machine of the SDPA protocol
  //
%}

%include <string.h>
%package sdpa::fsm
%class JobFSM
%header JobFSM.hpp

%start SMC_JobFSM::Pending
%map SMC_JobFSM

%%
Pending
{
    RunJob(event: sdpa::events::RunJobEvent&)
        Running
        {
        	action_run_job(event); 
        }

    CancelJob(event: sdpa::events::CancelJobEvent&)
        Cancelled
        {
           action_cancel_job(event); 
        }

    QueryJobStatus(event: sdpa::events::QueryJobStatusEvent&)
        nil
        {
            action_query_job_status(event);
        }
}

Running
{
    JobFinished(event: sdpa::events::JobFinishedEvent&)
      	Finished
        {
        	action_job_finished(event);
        }
      
  	JobFailed(event: sdpa::events::JobFailedEvent&)
    	Failed
        {
            action_job_failed(event);
        }
        
    CancelJob(event: sdpa::events::CancelJobEvent&)
        Cancelling
        {
			action_cancel_job(event);
        }

    QueryJobStatus(event: sdpa::events::QueryJobStatusEvent&)
        nil
        {
            action_query_job_status(event);
        }
}

Cancelling
{
    CancelJobAck(event: sdpa::events::CancelJobAckEvent&)
    	Cancelled
    	{
    		action_cancel_job_ack(event);
    	}
    	
	QueryJobStatus(event: sdpa::events::QueryJobStatusEvent&)
        nil
        {
        	action_query_job_status(event); 
        }
}

Finished
{
    QueryJobStatus(event: sdpa::events::QueryJobStatusEvent&)
        nil
        {
        	action_query_job_status(event); 
        }
        
	RetriveResultsEvent(event: sdpa::events::RetriveResultsEvent&)
        nil
        {
        	action_retrieve_job_results(event); 
        }        
}

Failed
{
    QueryJobStatus(event: sdpa::events::QueryJobStatusEvent&)
        nil
        {
        	action_query_job_status(event); 
        }
        
    RetriveResultsEvent(event: sdpa::events::RetriveResultsEvent&)
        nil
        {
        	action_retrieve_job_results(event); 
        }       
}

Cancelled
{
    QueryJobStatus(event: sdpa::events::QueryJobStatusEvent&)
        nil
        {
        	action_query_job_status(event); 
        }
}

%%
